WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

program = { SOI ~ stmt* ~ EOI }
stmt = _{ class_decl | assign_stmts | control_stmts | input_stmt | output_stmt | assert_stmt | method_stmts }


// ASSIGN
assign_stmts = _{ assign_stmt | increment_stmt | decrement_stmt }

assign_stmt = { ident ~ index? ~ assign_operation ~ expr }
increment_stmt = { ident ~ "++" }
decrement_stmt = { ident ~ "--" }

assign_operation = _{ assign | assign_add | assign_subtract | assign_multiply | assign_divide }
    assign = { "=" }
    assign_add = { "+=" }
    assign_subtract = { "-=" }
    assign_multiply = { "*=" }
    assign_divide = { "/=" }


// CONTROL
control_stmts = _{ if_stmt | while_loop_stmt | for_loop_stmt | loop_until_stmt }

if_stmt = { "if" ~ expr ~ "then"
    ~ stmt*
    ~ elif_clause*
    ~ else_clause?
    ~ "end" ~ "if"
}
elif_clause = { "else" ~ "if" ~ expr ~ "then" ~ stmt* }
else_clause = { "else" ~ stmt* }

while_loop_stmt = { "loop" ~ "while" ~ expr
    ~ stmt*
    ~ (("end" ~ "loop") | ("end" ~ "while"))
}
for_loop_stmt = { "loop" ~ ident ~ "from" ~ expr ~ "to" ~ expr
    ~ stmt*
    ~ "end" ~ "loop"
}
loop_until_stmt = { "loop" ~ "until" ~ expr
    ~ stmt*
    ~ "end" ~ "loop"
}


// METHOD
method_stmts = _{ method_decl | method_call | method_return }

method_decl = {
    "method" ~ ident ~  "(" ~ decl_param_list? ~ ")"
    ~ stmt*
    ~ "end" ~ "method"
}
decl_param_list = { ident ~ ("," ~ ident)* }

method_call = { ident ~ "(" ~ call_param_list? ~ ")" }
call_param_list = _{ expr ~ ("," ~ expr)* }

method_return = { "return" ~ expr }


// CLASS
class_decl = {
    "Class" ~ ident ~  "(" ~ decl_param_list? ~ ")"
    ~ (class_function | class_constructor_stmt)*
    ~ "end" ~ "class"
}
class_new = { "new" ~ ident ~ "(" ~ call_param_list? ~ ")" }


class_function = {
    class_ident ~ "=" ~ "function" ~ "(" ~ decl_param_list? ~ ")"
    ~ "{"
    ~ stmt*
    ~ "}"
}
class_ident = { "this" ~ "." ~ ident }
class_constructor_stmt = { class_ident ~ "=" ~ expr }

// EXPRESSION
expr = { logical_or }

logical_or = { logical_and ~ (or ~ logical_and)* }
logical_and = { comparison ~ (and ~ comparison)* }

comparison = { add_sub ~ ( (not_equal | greater_equal | less_equal | greater | less | equal) ~ add_sub )* }
add_sub = { mul_div ~ ( (add | subtract) ~ mul_div )* }
mul_div = { pow ~ ( (multiply | divide | int_divide | modulo) ~ pow )* }
pow = { unary ~ (power ~ pow)? } // right-association
unary = { (not | subtract)* ~ term }
    equal = { "==" | "=" }
    not_equal = { "!=" | "<>" }
    greater_equal = { ">=" }
    less_equal = { "<=" }
    greater = { ">" }
    less = { "<" }
    add = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide = { "/" }
    int_divide = { "div" }
    power = { "^" }
    modulo = { "%" | "mod" }
    and = { "&&" | "AND" }
    or = { "||" | "OR" }
    not = { "!" | "NOT" }


// NATIVE METHODS
input_call = { "input" ~ "(" ~ expr? ~ ")" }
div_call = { "div" ~ "(" ~ expr ~ "," ~ expr ~ ")" }
substring_call = { "." ~ "substring" ~ "(" ~ expr ~ "," ~ expr ~ ")" }
input_stmt = { "input" ~ ident }
output_stmt = { "output" ~ expr ~ ("," ~ expr)* }
assert_stmt = { "assert" ~ "(" ~ expr ~ "," ~ expr ~ ")" }

// FUNDAMENTALS
term = { primary ~ postfix* }
primary = { input_call | div_call | method_call | class_new | bool | array | ident | number | string | "(" ~ expr ~ ")" }
postfix = { substring_call | call | index }
call = { "." ~ ident ~ "(" ~ call_param_list? ~ ")" }
index = { "[" ~ expr ~ "]" }

array = { ("[" ~ (expr ~ ("," ~ expr)*)? ~ "]") | ("new" ~ "Array" ~ "()") } // Dirty hack

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ ("e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
int = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
bool = { "true" | "false" }