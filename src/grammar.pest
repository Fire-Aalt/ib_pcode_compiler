WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
end_line = _{ "\n"+ }

program = { SOI ~ (stmt | end_line)* ~ EOI }
stmt = _{ (class_decl |  assign_stmts | control_stmts | input_stmt | output_stmt | assert_stmt | method_stmts | class_function_call_stmt) ~ end_line }

// ASSIGN
assign_stmts = _{ assign_stmt | increment_stmt | decrement_stmt }

assign_stmt = { ident ~ index? ~ assign_operation ~ expr } // doesn't work for [0][1]
increment_stmt = { ident ~ "++" }
decrement_stmt = { ident ~ "--" }

assign_operation = _{ assign | assign_add | assign_subtract | assign_multiply | assign_divide }
    assign = { "=" }
    assign_add = { "+=" }
    assign_subtract = { "-=" }
    assign_multiply = { "*=" }
    assign_divide = { "/=" }


// CONTROL
control_stmts = _{ if_stmt | while_loop_stmt | for_loop_stmt | loop_until_stmt }

if_stmt = { "if" ~ expr ~ "then" ~ end_line
    ~ stmt*
    ~ elif_clause*
    ~ else_clause?
    ~ "end" ~ "if"
}
elif_clause = { "else" ~ "if" ~ expr ~ "then" ~ end_line
    ~ stmt*
}
else_clause = { "else" ~ end_line
    ~ stmt*
}

while_loop_stmt = { "loop" ~ "while" ~ expr ~ end_line
    ~ stmt*
    ~ (("end" ~ "loop") | ("end" ~ "while"))
}
for_loop_stmt = { "loop" ~ ident ~ "from" ~ expr ~ "to" ~ expr ~ end_line
    ~ stmt*
    ~ "end" ~ "loop"
}
loop_until_stmt = { "loop" ~ "until" ~ expr ~ end_line
    ~ stmt*
    ~ "end" ~ "loop"
}


// METHOD
method_stmts = _{ method_decl | method_call | method_return }

method_decl = {
    "method" ~ ident ~  "(" ~ decl_param_list? ~ ")" ~ end_line
    ~ stmt*
    ~ "end" ~ "method"
}
decl_param_list = { ident ~ ("," ~ ident)* }

method_call = { ident ~ "(" ~ call_param_list? ~ ")" }
call_param_list = _{ expr ~ ("," ~ expr)* }

method_return = { "return" ~ expr }


// CLASS
class_decl = {
    "Class" ~ ident ~  "(" ~ decl_param_list? ~ ")" ~ end_line
    ~ (class_function | class_constructor_stmt)*
    ~ "end" ~ "Class"
}
class_new = { "new" ~ ident ~ "(" ~ call_param_list? ~ ")" }
class_function_call_stmt = { expr }


class_function = {
    class_ident ~ "=" ~ "function" ~ "(" ~ decl_param_list? ~ ")" ~ end_line
    ~ "{" ~ end_line
    ~ stmt*
    ~ "}" ~ end_line
}
class_ident = { "this" ~ "." ~ ident }
class_constructor_stmt = { class_ident ~ "=" ~ expr ~ end_line }

// EXPRESSION
expr = { logical_or }

logical_or = { logical_and ~ (or ~ logical_and)* }
logical_and = { comparison ~ (and ~ comparison)* }

comparison = { add_sub ~ ( (not_equal | greater_equal | less_equal | greater | less | equal) ~ add_sub )* }
add_sub = { mul_div ~ ( (add | subtract) ~ mul_div )* }
mul_div = { pow ~ ( (multiply | divide | int_divide | modulo) ~ pow )* }
pow = { unary ~ (power ~ pow)? } // right-association
unary = { (not | subtract)* ~ term }
    equal = { "==" | "=" }
    not_equal = { "!=" | "<>" }
    greater_equal = { ">=" }
    less_equal = { "<=" }
    greater = { ">" }
    less = { "<" }
    add = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide = { "/" }
    int_divide = { "div" }
    power = { "^" }
    modulo = { "%" | "mod" }
    and = { "&&" | "AND" }
    or = { "||" | "OR" }
    not = { "!" | "NOT" }


// NATIVE METHODS
input_call = { "input" ~ "(" ~ expr? ~ ")" }
div_call = { "div" ~ "(" ~ expr ~ "," ~ expr ~ ")" }
substring_call = { "." ~ "substring" ~ "(" ~ expr ~ "," ~ expr ~ ")" }
input_stmt = { "input" ~ ident }
output_stmt = { "output" ~ expr ~ ("," ~ expr)* }
assert_stmt = { "assert" ~ "(" ~ expr ~ "," ~ expr ~ ")" }

// FUNDAMENTALS
term = { primary ~ postfix* }
primary = { input_call | div_call | method_call | bool | array | class_new | class_ident | ident | number | string | "(" ~ expr ~ ")" }
postfix = { substring_call | call | index }
call = { "." ~ ident ~ "(" ~ call_param_list? ~ ")" }
index = { "[" ~ expr ~ "]" }

array = { ("[" ~ (expr ~ ("," ~ expr)*)? ~ "]") | ("new" ~ "Array" ~ "()") } // Dirty hack

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ ("e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
int = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
bool = { "true" | "false" }